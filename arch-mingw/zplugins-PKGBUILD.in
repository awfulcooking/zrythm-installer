# Maintainer: Alexandros Theodotou <alex at zrythm dot org>
_pkgbase=zplugins
MINGW_PACKAGE_PREFIX=mingw-w64
pkgname=$MINGW_PACKAGE_PREFIX-$_pkgbase
pkgver=@VERSION@
pkgrel=1
arch=('any')
pkgdesc='a highly automated and intuitive digital audio workstation (mingw-w64)'
depends=("${MINGW_PACKAGE_PREFIX}-librsvg"
         "${MINGW_PACKAGE_PREFIX}-cairo")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-meson"
             "${MINGW_PACKAGE_PREFIX}-pkg-config")
options=('!strip' 'staticlibs')
license=("AGPL3")
url="https://www.zrythm.org"
source=("zplugins-${pkgver}.tar.gz")
sha256sums=('SKIP')
_architectures=('x86_64-w64-mingw32')

prepare() {
  rm -rf "$srcdir/zplugins-$pkgver"
  mv "${srcdir}/zplugins-v$pkgver" "$srcdir/zplugins-$pkgver"
}

build() {
  cd "${srcdir}/zplugins-${pkgver}"

  export NEED_WINE=1
  for _arch in "${_architectures[@]}"; do
    cd ext/Soundpipe && make CC=$_arch-gcc && cd ../..
    mkdir -p build-${_arch} && pushd build-${_arch}
    $_arch-meson \
      -Dtrial_ver=false \
      --buildtype=debugoptimized \
      --wrap-mode=forcefallback \
      ..
    ninja
    popd
  done
}

package() {
  export NEED_WINE=1
  for _arch in "${_architectures[@]}"; do
    DESTDIR="${pkgdir}" meson install -C ${srcdir}/zplugins-${pkgver}/build-${_arch}
  done
}
