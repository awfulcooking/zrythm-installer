# Maintainer: Alexandros Theodotou <alex at zrythm dot org>
_realname=jack2
_commit=5f1b2be0dc509469a072041643c3e45bfa406b9a
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.9.14.r1
pkgrel=1
arch=('any')
pkgdesc='jack2'
depends=("$MINGW_PACKAGE_PREFIX-portaudio")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-python3")
options=('strip' 'staticlibs')
license=("GPL2")
url="https://www.zrythm.org"
source=("https://github.com/jackaudio/$_realname/archive/$_commit.zip")
sha256sums=('SKIP')

prepare() {
  rm -rf ${_realname}-${pkgver}
  mv ${_realname}-${_commit} ${_realname}-${pkgver}
  cd ${_realname}-${pkgver}

  [[ -x ./waf ]] || /usr/bin/python ./bootstrap.py
  sed -i 's:bin/env python$:bin/env python:' waf
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  DEST_OS=win32 \
  TARGET=${MINGW_CHOST} \
  PKG_CONFIG=${MINGW_PREFIX}/bin/pkg-config \
  CC=gcc CXX=g++ PERL=/usr/bin/perl \
  AR=${MINGW_PREFIX}/bin/ar \
  WINDRES=${MINGW_PREFIX}/bin/windres \
  RST2MAN=${MINGW_PREFIX}/bin/rst2man3 \
  /usr/bin/python3 ./waf configure \
    --prefix=${MINGW_PREFIX} \
    --check-c-compiler=gcc \
    --check-cxx-compiler=g++
  /usr/bin/python ./waf build
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  export PYTHON=${MINGW_PREFIX}/bin/python
  /usr/bin/python ./waf install --destdir="${pkgdir}"
}
