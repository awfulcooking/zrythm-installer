# Maintainer: Alexandros Theodotou <alex at zrythm dot org>
_realname=lilv
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.24.8
pkgrel=1
arch=('any')
pkgdesc='lilv'
depends=("$MINGW_PACKAGE_PREFIX-sratom")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "python")
options=('strip' 'staticlibs')
license=("AGPL3")
url="https://www.zrythm.org"
source=("http://download.drobilla.net/$_realname-$pkgver.tar.bz2")
sha256sums=('SKIP')

prepare() {
  cd ${_realname}-${pkgver}

  [[ -x ./waf ]] || /usr/bin/python ./bootstrap.py
  sed -i 's:bin/env python$:bin/env python:' waf
  sed -i -e '40s/.*/#if 0/' src/util.c
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  DEST_OS=win32 \
  TARGET=${MINGW_CHOST} \
  PKG_CONFIG=${MINGW_PREFIX}/bin/pkg-config \
  CC=gcc PERL=/usr/bin/perl \
  AR=${MINGW_PREFIX}/bin/ar \
  WINDRES=${MINGW_PREFIX}/bin/windres \
  RST2MAN=${MINGW_PREFIX}/bin/rst2man3 \
  /usr/bin/python ./waf configure \
    --prefix=${MINGW_PREFIX} \
    --static --no-shared \
    --no-utils --no-bindings
  /usr/bin/python ./waf build
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  export PYTHON=${MINGW_PREFIX}/bin/python
  /usr/bin/python ./waf install --destdir="${pkgdir}"
}
