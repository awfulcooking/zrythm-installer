# Maintainer: Alexandros Theodotou <alex at zrythm dot org>
pkgname=zrythm
pkgver=@VERSION@
pkgrel=1
pkgdesc='a highly automated and intuitive digital audio workstation'
arch=('x86_64' 'i686')
url="https://www.zrythm.org"
license=('AGPL3')
depends=('gtk3' 'lilv' 'libx11' 'jack' 'libsndfile'
  'libyaml' 'libsamplerate' 'guile' 'gtksourceview4' 'alsa-lib'
  'fftw' 'graphviz' 'rtmidi' 'rtaudio' 'breeze-icons' 'zstd'
  'rubberband' 'xdg-utils' 'sdl2')
makedepends=(
  'python' 'gettext' 'sed'
  'ninja' 'help2man' 'python-sphinx'
  'ladspa' 'lv2')
optdepends=('portaudio: portaudio backend')
conflicts=( 'zrythm-git' )
source=( "https://www.zrythm.org/releases/zrythm-$pkgver.tar.gz" )
sha256sums=('SKIP')

_rootdir="zrythm-${pkgver}"

# FIXME reenable tests
build() {
  cd $_rootdir
  PKG_CONFIG_PATH=/usr/lib/zrythm/lib/pkgconfig \
    meson build --prefix=/usr -Dtests=false \
    -Dmanpage=true \
    -Dbuildtype=debugoptimized \
    -Dsdl=enabled -Drtaudio=enabled \
    -Drtmidi=enabled -Dcarla=enabled -Dlsp_dsp=enabled \
    -Dfallback_version=@VERSION@ \
    -Dtrial_ver=false
  ninja -C build
}

check() {
  cd $_rootdir
  ninja -C build test
}

package() {
  cd $_rootdir
  install -vDm 644 AUTHORS CONTRIBUTING.md CHANGELOG.md README.md THANKS TRANSLATORS \
    -t "${pkgdir}/usr/share/doc/zrythm/"
  DESTDIR="${pkgdir}/" ninja -C build install
  mkdir -p "${pkgdir}/usr/lib/zrythm/lib"
  cp -R /usr/lib/zrythm/lib/carla ${pkgdir}/usr/lib/zrythm/lib/
}
