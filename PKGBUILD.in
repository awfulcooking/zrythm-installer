# Maintainer: Alexandros Theodotou <alex at zrythm dot org>
pkgname=zrythm
pkgver=@VERSION@
pkgrel=1
pkgdesc='a highly automated and intuitive digital audio workstation'
arch=('x86_64' 'i686')
url="https://www.zrythm.org"
license=('AGPL3')
depends=('gtk3' 'lilv' 'libx11' 'jack' 'libsndfile'
  'libyaml' 'libsamplerate' 'guile' 'gtksourceview4' 'alsa-lib'
  'fftw'
  'rubberband' 'xdg-utils' 'sdl2')
makedepends=(
  'python' 'gettext' 'sed'
  'meson' 'ninja' 'help2man' 'python-sphinx'
  'ladspa' 'lv2')
optdepends=('portaudio: portaudio backend')
conflicts=( 'zrythm-git' )
source=( "https://www.zrythm.org/releases/zrythm-$pkgver.tar.xz" )
sha256sums=('SKIP')

_rootdir="zrythm-${pkgver}"

build() {
  cd $_rootdir
  meson build --prefix=/usr -Denable_tests=true \
    -Dmanpage=true \
    -Dbuildtype=release -Denable_ffmpeg=true \
    -Denable_sdl=true -Denable_rtaudio=true \
    -Denable_rtmidi=true
  ninja -C build
}

check() {
  cd $_rootdir
  ninja -C build test
}

package() {
  cd $_rootdir
  install -vDm 644 AUTHORS CONTRIBUTING.md CHANGELOG.md README.md THANKS TRANSLATORS \
    -t "${pkgdir}/usr/share/doc/zrythm/"
  DESTDIR="${pkgdir}/" ninja -C build install
}
